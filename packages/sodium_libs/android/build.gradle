group = "de.skycoder42.sodium_libs"
version = "3.4.6+4"

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def sdkDir = properties.getProperty('flutter.sdk')
OperatingSystem os = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem;

android {
    namespace = "de.skycoder42.sodium_libs"

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdk = 21
    }

    task downloadLibsodium(type: Exec) {
        if (os.isWindows()) {
            executable sdkDir + "\\bin\\dart.bat"
        } else {
            executable sdkDir + "/bin/dart"
        }
        args "run", "../tool/libsodium/download.dart", "android"
    }
    preBuild.dependsOn downloadLibsodium
}
